CREATE PROCEDURE [dbo].[uspWSOL_SSRS_NAME_ADDRESS_UPDATES]
 @DATE_BEG			DATETIME
,@DATE_END			DATETIME
,@TYPE_OF_CHANGE	VARCHAR(200) --Vis. Def='ALL'
,@W9				VARCHAR(200) --Vis. Def='Active'
,@PSW				VARCHAR(10)  --Hid. Def='NONE'
,@EXECUTIONER		VARCHAR(3)   --Hid. Def='MAN'  MANual,SUBscription
,@RPT_TYPE			VARCHAR(3)   --Hid. Def='DLY'  
AS
BEGIN
SET NOCOUNT ON 

--  EXECUTE [dbo].[uspWSOL_SSRS_NAME_ADDRESS_UPDATES] '06/18/2017','06/18/2017','ALL','ALL','','MAN','WKL'

--=============================================================================
SET DATEFIRST 1  --1=MONDAY  7=SUNDAY  --DW = M,T,W,T,F,S,S
--=============================================================================
DECLARE
 @DTM_BEG AS DATETIME
,@DTM_END AS DATETIME
	IF @DATE_BEG < '01/01/1901' OR @RPT_TYPE = 'WKL' BEGIN	--WEEKLY - Run report for 7 days from current day to current day.
			--Dates passed in are for PREVIOUS DAY:
		SET @DTM_BEG = CAST(CONVERT(VARCHAR(10),@DATE_BEG,101) AS DATETIME)				-- @DATE_BEG already starting at yesterday's date.
		SET @DTM_BEG = CASE WHEN DATEPART(DW,@DTM_BEG) = 1 THEN @DTM_BEG				-- MONDAY
							ELSE DATEADD(DD,-6,@DTM_BEG) END								-- SET START DATE TO PAST MONDAY
		SET @DTM_END = CAST(CONVERT(VARCHAR(10),@DATE_END,101) AS DATETIME)				-- + 1 --DONE BELOW. 

		IF @DTM_END >  DBO.GETDATE() - 1 BEGIN											--DBO.GETDATE() -1 IS GOING TO ALSO INCLUDE SOME TIME AFTER MIDNIGHT.  THIS IS CORRECT !
			SET @DTM_END = CAST(CONVERT(VARCHAR(10),DBO.GETDATE() - 1,101) AS DATETIME)
		END
	END
	ELSE BEGIN  --DAILY - Run report for YESTERDAY, 1 Day ONLY.
		SET @DTM_BEG = CAST(CONVERT(VARCHAR(10),@DATE_BEG,101) AS DATETIME)				--@DATE_BEG already starting at yesterday's date.
		SET @DTM_END = CAST(CONVERT(VARCHAR(10),@DATE_END,101) AS DATETIME)				-- + 1 --done below.
	END

SET @DTM_END    = @DTM_END + 1  --EVERYTHING ABOVE, @DTM_END IS FOR EXACT DATE RANGE NEEDED. ONE IS ADDED SO WHERE CLAUSE " < @DTM_END" WORKS.

--=======================================================================================
--	SET MULTI-VALUE PARAMETERS:
--=======================================================================================
SET @TYPE_OF_CHANGE = ',' + LTRIM(RTRIM(REPLACE(@TYPE_OF_CHANGE	,'ALL','0'))) + ','
SET @W9				= ',' + LTRIM(RTRIM(REPLACE(@W9				,'ALL','0'))) + ','

--==================================
--	CREATE TMP TABLE:
--==================================
--	SELECT TOP 100 * FROM NAMECHAGEHISTTORY
IF OBJECT_ID('TEMPDB..#WSOL_SSRS_NAME_ADDRESS_UPDATES') IS NOT NULL BEGIN
   DROP TABLE #WSOL_SSRS_NAME_ADDRESS_UPDATES
END
CREATE TABLE #WSOL_SSRS_NAME_ADDRESS_UPDATES
(FIRST_NAME						VARCHAR(100)
,MIDDLE_INITIAL					VARCHAR(100)
,LAST_NAME						VARCHAR(100)
,CORP_NAME						VARCHAR(4000)  
,FIRST_NAME_OLD					VARCHAR(100)
,MIDDLE_INITIAL_OLD				VARCHAR(100)
,LAST_NAME_OLD					VARCHAR(100)  
,CORP_NAME_OLD					VARCHAR(4000)
,[EMAIL]						VARCHAR(1000)
,EMAIL_OLD						VARCHAR(1000)
,MAIL_ADDRESS					VARCHAR(4000)
,MAIL_ADDRESS_OLD				VARCHAR(4000)
,CITY							VARCHAR(300)
,CITY_OLD						VARCHAR(300)
,ID_STATE						VARCHAR(2)
,STATE_NAME						VARCHAR(100)
,ID_STATE_OLD					VARCHAR(2)
,STATE_NAME_OLD					VARCHAR(100)
,ZIP							VARCHAR(20)
,ZIP_OLD						VARCHAR(20)
,CREATED_DATE					DATETIME
,[DATETIME]						DATETIME
,RESOURCE_NAME					VARCHAR(200)
,TYPE_OF_CHANGE					VARCHAR(20)
,APPLICANT_W9					VARCHAR(1)
,APPLICANT_CORP_W9				VARCHAR(1)
,APPLICANT_W9_STATUS			VARCHAR(20)
)

INSERT INTO #WSOL_SSRS_NAME_ADDRESS_UPDATES
SELECT
 DAT.FIRST_NAME
,DAT.MIDDLE_INITIAL
,DAT.LAST_NAME
,DAT.CORP_NAME
,DAT.FIRST_NAME_OLD
,DAT.MIDDLE_INITIAL_OLD
,DAT.LAST_NAME_OLD
,DAT.CORP_NAME_OLD
,DAT.EMAIL
,DAT.EMAIL_OLD
,DAT.MAIL_ADDRESS
,DAT.MAIL_ADDRESS_OLD
,DAT.CITY
,DAT.CITY_OLD
,DAT.ID_STATE
,DAT.STATE_NAME
,DAT.ID_STATE_OLD
,DAT.STATE_NAME_OLD
,DAT.ZIP
,DAT.ZIP_OLD
,DAT.CREATED_DATE
,DAT.[DATETIME]
,DAT.RESOURCE_NAME
,DAT.TYPE_OF_CHANGE
,DAT.APPLICANT_W9
,DAT.APPLICANT_CORP_W9
,DAT.APPLICANT_W9_STATUS
FROM
(	SELECT	
	 RTRIM(LTRIM(ISNULL(NCH.FIRSTNAME		,'')))	AS FIRST_NAME
	,RTRIM(LTRIM(ISNULL(NCH.MIDDLINIT		,'')))	AS MIDDLE_INITIAL
	,RTRIM(LTRIM(ISNULL(NCH.LASTNAME		,'')))	AS LAST_NAME
	,RTRIM(LTRIM(ISNULL(NCH.CORPORATNAME	,'')))	AS CORP_NAME
	,RTRIM(LTRIM(ISNULL(NCH.FIRSTNAMEOLD	,'')))	AS FIRST_NAME_OLD
	,RTRIM(LTRIM(ISNULL(NCH.MIDDLINITOLD	,'')))	AS MIDDLE_INITIAL_OLD
	,RTRIM(LTRIM(ISNULL(NCH.LASTNAMEOLD		,'')))	AS LAST_NAME_OLD
	,RTRIM(LTRIM(ISNULL(NCH.CORPORATNAMEOLD	,'')))	AS CORP_NAME_OLD
	,RTRIM(LTRIM(ISNULL(NCH.EMAIL			,'')))	AS [EMAIL]
	,RTRIM(LTRIM(ISNULL(NCH.EMAILOLD		,'')))	AS EMAIL_OLD
	,RTRIM(LTRIM(ISNULL(NCH.MAILADDR		,'')))	AS MAIL_ADDRESS
	,RTRIM(LTRIM(ISNULL(NCH.MAILADDROLD		,'')))	AS MAIL_ADDRESS_OLD
	,RTRIM(LTRIM(ISNULL(NCH.CITY			,'')))	AS CITY
	,RTRIM(LTRIM(ISNULL(NCH.CITYOLD			,'')))	AS CITY_OLD
	,NCH.IDSTATE									AS ID_STATE
	,RTRIM(LTRIM(ST1.[NAME]))						AS STATE_NAME
	,NCH.IDSTATEOLD									AS ID_STATE_OLD
	,RTRIM(LTRIM(ST2.[NAME]))						AS STATE_NAME_OLD
	,RTRIM(LTRIM(ISNULL(NCH.ZIP				,'')))	AS ZIP
	,RTRIM(LTRIM(ISNULL(NCH.ZIPOLD			,'')))	AS ZIP_OLD
	,NCH.CREATEDDATE								AS CREATED_DATE
	,CAST(LEFT(NCH.CREATEDDATE,10) AS DATETIME)		AS [DATETIME]
	,CASE WHEN ISNULL(NCH.MIDDLINIT,'') = '' THEN
			CONCAT(NCH.FIRSTNAME,' ',NCH.LASTNAME)
		  ELSE
			CONCAT(NCH.FIRSTNAME,' ',NCH.MIDDLINIT,' ',NCH.LASTNAME)
		  END										AS RESOURCE_NAME
	,CASE WHEN (	NCH.FIRSTNAME		<> NCH.FIRSTNAMEOLD
				 OR NCH.MIDDLINIT		<> NCH.MIDDLINITOLD
				 OR NCH.LASTNAME		<> NCH.LASTNAMEOLD
				 OR NCH.CORPORATNAME	<> NCH.CORPORATNAMEOLD
			   ) THEN 'NAMES'
		  WHEN		NCH.EMAIL			<> NCH.EMAILOLD
				 THEN 'EMAIL'
		  WHEN (	NCH.MAILADDR		<> NCH.MAILADDROLD
				 OR NCH.CITY			<> NCH.CITYOLD
				 OR NCH.IDSTATE			<> NCH.IDSTATEOLD
				 OR NCH.ZIP				<> NCH.ZIPOLD
			   ) THEN 'STREET'
		  ELSE ''
	 END											AS TYPE_OF_CHANGE
	,APP.W9											AS APPLICANT_W9
	,APP.CORPORATEW9								AS APPLICANT_CORP_W9
	,CASE WHEN (	APP.W9			= 'Y'
				 OR APP.CORPORATEW9	= 'Y'
			   ) THEN 'ACTIVE'
		  ELSE 'INACTIVE'
	 END											AS APPLICANT_W9_STATUS
	--  SELECT *
	FROM		NAMECHAGEHISTTORY	NCH
	LEFT JOIN	[STATE]				ST1		ON ST1.IDSTATE		= NCH.IDSTATE
	LEFT JOIN	[STATE]				ST2		ON ST2.IDSTATE		= NCH.IDSTATEOLD
	LEFT JOIN	APPLICANT			APP		ON APP.IDAPPLICANT	= NCH.IDAPPLICANT

	WHERE NCH.CREATEDDATE >= @DTM_BEG AND NCH.CREATEDDATE <  @DTM_END
) DAT

--=================================================================================================
--	RETURN DATASET:
--=================================================================================================


SELECT 
 CONVERT(VARCHAR(10),@DTM_BEG,101) + '-' + CONVERT(VARCHAR(10),@DTM_END - 1,101)  AS DATE_RANGE
,V.*
FROM	#WSOL_SSRS_NAME_ADDRESS_UPDATES		V
WHERE	(@TYPE_OF_CHANGE IN (',0,') OR CHARINDEX(',' + RTRIM(V.TYPE_OF_CHANGE)		+ ',',@TYPE_OF_CHANGE) > 0)
  AND	(@W9			 IN (',0,') OR CHARINDEX(',' + RTRIM(V.APPLICANT_W9_STATUS) + ',',@W9			 ) > 0)
ORDER BY 
 V.CREATED_DATE
,V.RESOURCE_NAME

--===============================
EARLY_EXIT:
--===============================
--IMPOSSIBLE:  SELECT 1/0    POSSIBLE:  SELECT 0/1

END

GO